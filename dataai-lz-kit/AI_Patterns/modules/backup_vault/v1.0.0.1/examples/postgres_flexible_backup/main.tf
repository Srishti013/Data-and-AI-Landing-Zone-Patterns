terraform {
  required_version = ">= 1.7.0"

  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 4.0"
    }
    random = {
      source  = "hashicorp/random"
      version = ">= 3.5.0, < 4.0"
    }
  }
}

provider "azurerm" {
  features {}
}

# Generate a secure password for PostgreSQL
resource "random_password" "postgres_password" {
  length           = 16
  min_lower        = 2
  min_numeric      = 2
  min_special      = 2
  min_upper        = 2
  override_special = "!@#$%&*()-_=+[]{}<>:?"
  special          = true
}

# Create a Resource Group
resource "azurerm_resource_group" "example" {
  location = "centralus"
  name     = "rg-postgres-flex-backup-tst"
}

# Create a PostgreSQL Flexible Server
resource "azurerm_postgresql_flexible_server" "example" {
  location               = azurerm_resource_group.example.location
  name                   = "psql-flex-backup-tst"
  resource_group_name    = azurerm_resource_group.example.name
  administrator_login    = "psqladmin"
  administrator_password = random_password.postgres_password.result
  # Add geo-redundant backup for disaster recovery
  geo_redundant_backup_enabled = true
  sku_name                     = "GP_Standard_D4s_v3"
  storage_mb                   = 32768
  version                      = "12"
  zone                         = "1"

  # Configure high availability with zone redundancy
  high_availability {
    mode                      = "ZoneRedundant"
    standby_availability_zone = "2"
  }
  # Define a custom maintenance window
  maintenance_window {
    day_of_week  = "4" # Thursday
    start_hour   = 2   # 2 AM, adjusted to off-peak time
    start_minute = 34
  }
}

# Call PostgreSQL Flexible Backup Vault and Backup Policy
module "backup_vault" {
  source = "../../"

  # Required naming and tag variables
  env            = "tst"
  au             = "0000001"
  owner          = "ITOpsTeam"
  app_code       = "bkp"
  bu             = "IT"
  app_name       = "backup-test"
  business_unit  = "IT"
  business_owner = "admin@example.com"
  service        = "backup"
  budget_id      = "BUD001"
  criticality    = "Low"
  environment    = "Testing"

  datastore_type      = "VaultStore"
  redundancy          = "LocallyRedundant"
  resource_group_name = azurerm_resource_group.example.name
  backup_instances = {
    postgresqlflex = {
      type                          = "postgresql_flexible"
      name                          = "psql-flex-backup-tst-postgresflex-instance"
      backup_policy_key             = "postgresqlflex"
      postgresql_flexible_server_id = azurerm_postgresql_flexible_server.example.id
    }
  }
  backup_policies = {
    postgresqlflex = {
      type                            = "postgresql_flexible"
      name                            = "psql-flex-backup-tst-backup-policy"
      backup_repeating_time_intervals = ["R/2024-09-17T06:33:16+00:00/PT4H"]
      default_retention_duration      = "P4M"
      retention_rules = [
        {
          name     = "Daily"
          duration = "P7D"
          priority = 25
          criteria = [{ absolute_criteria = "FirstOfDay" }]
        }
      ]
      time_zone = "UTC"
    }
  }
  enable_telemetry = true
  managed_identities = {
    system_assigned = true
  }
  # Role assignments using placeholder value that will be replaced by the module
  # This avoids circular references while keeping assignments inside the module
  role_assignments = {
    postgresql_contributor = {
      principal_id               = "system-assigned" # Special marker that the module will interpret
      role_definition_id_or_name = "Contributor"
      scope                      = azurerm_postgresql_flexible_server.example.id
      description                = "Allow backup vault identity to perform backup operations on PostgreSQL Flexible server"
    }
    resource_group_reader = {
      principal_id               = "system-assigned" # Special marker that the module will interpret
      role_definition_id_or_name = "Reader"
      scope                      = azurerm_resource_group.example.id
      description                = "Allow backup vault identity to read resource group information"
    }
  }
}
